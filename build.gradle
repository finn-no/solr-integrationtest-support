plugins {
    id 'java'
    id 'idea'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.0'
    id 'net.researchgate.release' version '2.8.1'

}

ext.versions = [
    junit   : '4.13',
    junit5   : '5.6.0',
    solr    : '8.4.1',
    hamcrest: '2.2'
]

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    jcenter()
    mavenLocal()
}


dependencies {
    compile "org.apache.solr:solr-solrj:${versions.solr}"
    compile "org.apache.solr:solr-core:${versions.solr}"
    compile "org.hamcrest:hamcrest:${versions.hamcrest}"
    compile "org.junit.jupiter:junit-jupiter-api:${versions.junit5}"
    compile "org.junit.jupiter:junit-jupiter-engine:${versions.junit5}"
}

test {
    useJUnitPlatform()
}


tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

project.afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                groupId 'no.finn.search'
                artifactId 'solr-integrationtest-support'
                version "${project.version}"
            }
        }
    }
}


bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    pkg {
        repo = 'search'
        name = 'solr-integrationtest-support'
        userOrg = "finn-no"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/finn-no/solr-integrationtest-support'
        labels = ['solr', 'integrationtest', 'junit', 'spock']
        publicDownloadNumbers = true
        version {
            name = "${project.version}"
            desc = 'Solr Integration test support - Simple testing for solrconfig and schemas'
            released = new Date()
            vcsTag = "${project.version}"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    sourcesJar
    javadocJar
}


wrapper {
    gradleVersion = '6.1.1'
}

idea {
    project {
        languageLevel = '1.8'
    }
}
